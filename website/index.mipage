:Class index : MiPage

    ∇ Compose;go;mat
    ⍝ Create initial page content
      :Access Public
     
      Add _.StyleSheet'https://www.w3.org/StyleSheets/Core/Chocolate'
      Add _.title'Digit Frequency'
      Add _.h1'Digit Frequency Analysis'
     
      file←'http://samplecsvs.s3.amazonaws.com/SacramentocrimeJanuary2006.csv'
      '#file' 'style="width:600px;"' Add _.Input 'text' file 'Filename: '
      Add '<br/><br/>'
      btnstyle←'style="padding:5px 25px; background-color:orange;"'
      go←btnstyle Add _.Button 'Run Analysis'
      go.On'click' 'OnGo'('input' 'val' '#file')
      Add '<br/><br/>'
      mat←'Digit' 'Count'⍪(⍪⎕D),0
      '#freqtable'Add _.Table mat ⍬ 1

      Add '<br/>'
      '#chart' 'style="width:50%"'Add _.div
    ∇

    ∇ r←OnGo;mat;svg;data;file;z;engine
      :Access Public
     
      :Trap 0
         file←Get'input'
         engine←2 ⎕NQ '.' 'GetEnvironment' 'ENGINE' ⍝ get engine address from environment
         engine,←(0=≢engine)/'172.27.119.242:8081'       ⍝ default to Mary's Linux machine
         z←#.HttpCommand.GetJSON'post' (engine,'/Analyze') file
         mat←'' 'Count'⍪(⎕D,¨':'),⍪↓'CI11' ⎕FMT data←z.Data
         r←'#freqtable'Replace New _.Table mat ⍬ 1
         svg←Chart data
         r,←'#chart'Replace svg
      :Else
         r←'#freqtable'Replace 'ERROR'
         r,←'#chart'Replace '<pre>',(,⍕((⊂⍕z),⎕DM),¨⊂'<br>'),'</pre>'
      :EndTrap
    ∇

    ∇ svg←Chart data;sp
      InitCauseway
      sp←⎕NEW Causeway.SharpPlot
      sp.YAxisStyle←Causeway.YAxisStyles.ForceZero
      sp.DrawBarChart data
      svg←sp.RenderSvg Causeway.SvgMode.FixedAspect
    ∇

    ∇ InitCauseway
      :If 0=⎕NC'Causeway'
          ⎕USING←',system.dll' ',system.drawing.dll' ',sharpplot.dll' ⍝ Try to get .NET version of Causeway
      :AndIf 0=⎕NC'Causeway'                                          ⍝ Else use the APL version
          (System.Drawing←System←Causeway←⎕NS ⍬).⎕CY'/opt/mdyalog/17.1/64/unicode/ws/sharpplot.dws'
      :EndIf
    ∇

:EndClass
